plugins {
    id 'application' // run task
    id 'checkstyle' // code formatting
    id 'jacoco' // coverage
    id 'java'
    id 'com.github.spotbugs' version '2.0.0'
}

checkstyle {
    toolVersion '8.25'
    configFile file("config/checkstyle/google_checks.xml")
}

spotbugs {
    toolVersion = '3.1.9'
    effort = 'max'
    reportLevel = 'low'
}

jacocoTestReport {
    reports {
        xml.enabled false
        csv.enabled false
        html.destination file("${buildDir}/jacocoHtml")
    }
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 0.90
            }
        }
        rule {
            limit {
                counter = 'BRANCH'
                value = 'COVEREDRATIO'
                minimum = 0.80
            }
        }
    }
}

repositories {
    mavenCentral()
}

task unitTest(type: Test) {
    group 'Verification'
    description 'Runs all unit tests'
    include '**/UnitTestSuite.class'  
}

task integrationTest(type: Test) {
    group 'Verification'
    description 'Runs all integration tests'
    include '**/IntegrationTestSuite.class'  
}

task functionalTest(type: Test) {
    group 'Verification'
    description 'Runs all functional tests'
    include '**/FunctionalTestSuite.class'  
}

test {
    group 'Verification'
    useJUnitPlatform() // jupiter
}

task release() {
    group 'Verification'
    description 'runs all tests validates code coverage'
    
    dependsOn check
    dependsOn test
    dependsOn jacocoTestReport
    dependsOn jacocoTestCoverageVerification
}

ext {
   javaMainClass = "com.tesla.interview.application.cli.CommandLineInterviewApplication"
}

application {
    mainClassName = javaMainClass
}

version = 1.0
sourceCompatibility = 1.8

dependencies {
    implementation 'com.beust:jcommander:1.78'
    implementation 'com.google.guava:guava:28.1-jre'
    implementation 'com.github.spotbugs:spotbugs-annotations:3.1.9'
    implementation 'io.prometheus:simpleclient:0.8.0'
    implementation 'io.prometheus:simpleclient_pushgateway:0.8.0'
    implementation 'org.apache.logging.log4j:log4j-api:2.12.1'
    
    runtimeOnly 'io.prometheus:simpleclient_log4j2:0.8.0'
    runtimeOnly 'org.apache.logging.log4j:log4j-core:2.12.1'
    
    testImplementation 'org.apache.logging.log4j:log4j-api:2.12.1'
    testImplementation 'org.mockito:mockito-core:3.1.0'
    testImplementation 'org.junit.platform:junit-platform-runner:1.5.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.5.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.5.2'
    
    testRuntimeOnly 'org.apache.logging.log4j:log4j-core:2.12.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.5.2'
}
